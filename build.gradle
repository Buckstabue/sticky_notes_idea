plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.4'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id "org.jetbrains.kotlin.kapt" version '1.4.10'
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}

Properties localProps = new Properties()
def propertiesFile = project.rootProject.file('gradle-local.properties')
if (propertiesFile.exists()) {
    localProps.load(propertiesFile.newDataInputStream())
}

group 'com.buckstabue'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

apply plugin: 'kotlin-kapt'

dependencies {
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-swing:1.3.4"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4"
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'com.google.dagger:dagger:2.25.2'
    implementation ('io.sentry:sentry:1.7.27') {
        exclude group: 'org.slf4j'
    }
    implementation('com.brsanthu:google-analytics-java:2.0.0') {
        exclude group: 'org.slf4j'
    }
    kapt 'com.google.dagger:dagger-compiler:2.25.2'
    testCompile "junit:junit:4.12"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '201.8743.12'
    plugins 'git4idea'
    updateSinceUntilBuild false
}
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

patchPluginXml {
    changeNotes """
      Initial version"""
}

buildConfig {
    packageName = "com.buckstabue.stickynotes"

    buildConfigField 'String', 'GA_ACCOUNT_ID', localProps.getProperty("ga_id", "")
    buildConfigField 'String', 'SENTRY_DSN', localProps.getProperty("sentry_dsn", "")
    buildConfigField 'String', 'ENVIRONMENT', localProps.getProperty("environment", "debug")
}

sourceSets.main.java {
    srcDir "$buildDir/gen/buildconfig/src/main"
}
